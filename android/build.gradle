buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["SnapchatCameraKit_kotlinVersion"]

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

def appProject = rootProject.allprojects.find { it.plugins.hasPlugin('com.android.application') }

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["SnapchatCameraKit_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["SnapchatCameraKit_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  if (major == 7 && minor >= 3) {
    return true
  }

  return major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.snapchatcamerakit"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")
def camerakit_version = getExtOrDefault("cameraKitVersion")

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion

  // workaround: https://github.com/callstack/react-native-builder-bob/issues/446
  // below code block needs to be added for linting
  def isLinting = project.gradle.startParameter.taskNames.contains('lint')
  if (isLinting) {
    implementation "com.facebook.react:react-android:+"
  } else {
    implementation "com.facebook.react:react-native:+"
  }

  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"

  // The main Camera Kit artifact that provides all the core functionality.
  implementation "com.snap.camerakit:camerakit:$camerakit_version"

  // Implementation of Camera pipeline for Camera Kit using CameraX library
  implementation "com.snap.camerakit:support-camerax:$camerakit_version"

  // Camera Kit ring light support
  implementation "com.snap.camerakit:support-snap-flash:$camerakit_version"

  // Optional dependency that provides ArCore backed Source<ImageProcessor> to enable
  // depth and other ArCore specific features in lenses that require them.
  // Used by the support-camera-layout but can be safely removed if not needed.
  implementation "com.snap.camerakit:support-arcore:$camerakit_version"

  implementation "com.snap.camerakit:support-camera-layout:$camerakit_version"

  implementation 'com.github.wseemann:FFmpegMediaMetadataRetriever-core:1.0.19'
  implementation 'com.github.wseemann:FFmpegMediaMetadataRetriever-native:1.0.19'
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "SnapchatCameraKitView"
    codegenJavaPackageName = "com.snapchatcamerakit"
  }
}
